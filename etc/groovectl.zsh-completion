#compdef groovectl

# groovectl zsh completion
# Copyright (C) 2014 Alessandro Ghedini <alessandro@ghedini.me>
# This file is released under the 2 clause BSD license, see COPYING

_arguments                      \
  ':subcommand:->subcommand'    \
  '*:args:->args'

case $state in
  subcommand)
    local -a subcommands
    subcommands=(
      "add:append tracks to the player's tracklist"
      "goto:skip to a specific track in the tracklist"
      "ls:show tracklist"
      "load:load a playlist file"
      "loop:set the player's loop mode"
      "next:skip to next track"
      "pause:pause the player"
      "play:unpause the player"
      "prev:skip to previous track"
      "quit:shutdown the player"
      "rm:remove a track from the tracklist"
      "save:save the tracklist to a playlist file"
      "seek:seek by a relative amount of seconds"
      "status:show the status of the player"
      "stop:stop playback and clear tracklist"
      "toggle:toggle the player's pause status"
    )

    _describe -t subcommands 'groovectl subcommand' subcommands
  ;;

  args)
    case $words[2] in
      add)
        _files
      ;;

      load)
        _files
      ;;

      loop)
        local -a values
        values=(
          "track:loop current track"
          "list:loop whole tracklist"
          "none:disable looping"
          "force:loop whole tracklist without skipping failing entries"
        )

        _describe -t values 'groovectl loop values' values
      ;;

      save)
        _files
      ;;
    esac
  ;;
esac
