#compdef groovectl

# groovectl zsh completion
# Copyright (C) 2014 Alessandro Ghedini <alessandro@ghedini.me>
# This file is released under the 2 clause BSD license, see COPYING

_arguments                      \
  ':subcommand:->subcommand'    \
  '*:args:->args'

case $state in
  subcommand)
    local -a subcommands
    subcommands=(
      "add:append tracks to the player's tracklist"
      "last:stop playback after currently playing track"
      "list:show tracklist"
      "loop:set the player's loop mode"
      "lyrics:download and show lyrics for the currently playing track"
      "next:skip to next track"
      "pause:pause the player"
      "play:unpause the player"
      "prev:skip to previous track"
      "quit:shutdown the player"
      "rgain:set the player's replaygain mode"
      "seek:seek by a relative amount of seconds"
      "status:show the status of the player"
      "stop:stop playback and clear tracklist"
      "toggle:toggle the player's pause status"
    )

    _describe -t subcommands 'groovectl subcommand' subcommands
  ;;

  args)
    case $words[2] in
      add)
        _files
      ;;

      rgain)
        local -a values
        values=(
          "track:track mode"
          "album:album mode"
          "none:disable replaygain"
        )

        _describe -t values 'groovectl rgain values' values
      ;;

      loop)
        local -a values
        values=(
          "on:enable loop mode"
          "off:disable loop mode"
        )

        _describe -t values 'groovectl loop values' values
      ;;
    esac
  ;;
esac
